module linear.Typecheck where

open import Function
open import Data.Nat
open import Data.Fin
open import Data.Vec hiding ([_] ; _++_ ; tail)
open import Data.Product as P using (projâ‚ ; projâ‚‚)
open import Relation.Nullary
open import Relation.Binary.PropositionalEquality

open import linear.Type as Type
open import linear.Context as C hiding (_++_)
open import linear.Usage
open import linear.Usage.Equality as UsageEq
open import linear.Language
open import linear.Typing
open import linear.Typing.Inversion
open import linear.Typing.Functional
open import linear.RawIso
open import linear.Typecheck.Problem

-- Decidability of Type-checking

consume : {n : â„•} {Î³ : Context n} (Î“ : Usages Î³) (k : Fin n) â†’ Dec $ CONSUME Î“ k
consume ([ Ïƒ ] âˆ· Î“) zero    = yes (Ïƒ , ] Ïƒ [ âˆ· Î“ , z)
consume (] Ïƒ [ âˆ· Î“) zero    = no (Î» { (_ , _ , ()) })
consume (Ïƒ âˆ· Î“)     (suc k) = consumeSuc Î“ Ïƒ k <$> consume Î“ k

checkPattern : {n : â„•} (Ïƒ : Type) (p : Pattern n) â†’ Dec $ PATTERN Ïƒ p
checkPattern Ïƒ `v              = yes (Ïƒ âˆ· [] , `v)
checkPattern ğŸ™        (p ,, q) = no (Î» { (_ , ()) })
checkPattern (Ïƒ âŠ— Ï„)  (p ,, q) = patternTensor <$> checkPattern Ïƒ p <*> checkPattern Ï„ q
checkPattern (Ïƒ â”€o Ï„) (p ,, q) = no (Î» { (_ , ()) })
checkPattern (Ïƒ & Ï„)  (p ,, q) = no (Î» { (_ , ()) })
checkPattern (Îº x)    (p ,, q) = no (Î» { (_ , ()) })
checkPattern (Ïƒ âŠ• Ï„)  (p ,, q) = no (Î» { (_ , ()) })


truncate : {n o : â„•} {Î³ : Context n} (Î´ : Context o) (Î“ : Usages (Î´ C.++ Î³)) â†’ Dec $ TRUNCATE Î´ Î“
truncate []      Î“            = yes (Î“ , refl)
truncate (a âˆ· Î´) ([ .a ] âˆ· Î“) = no (Î» { (_ , ()) })
truncate (a âˆ· Î´) (] .a [ âˆ· Î“) = truncateUsed Î´ Î“ <$> truncate Î´ Î“

mutual

  infer : {n : â„•} {Î³ : Context n} (Î“ : Usages Î³) (t : Infer n) â†’ Dec $ INFER Î“ t

  -- VAR
  infer Î“ (`var k)                     = inferVar Î“ k <$> consume Î“ k

  -- APP
  infer Î“ (`app t u)
    with infer Î“ t
  ... | no Â¬p = no $ Î» p â†’ Â¬p (_ , _ , app-inv-function (INFER.proof p))
  ... | yes (ğŸ™ , _ , T)     = no $ Î» p â†’ case functionalInfer _ T (app-inv-function $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ âŠ— Ï„ , _ , T) = no $ Î» p â†’ case functionalInfer _ T (app-inv-function $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ âŠ• Ï„ , _ , T) = no $ Î» p â†’ case functionalInfer _ T (app-inv-function $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ & Ï„ , _ , T) = no $ Î» p â†’ case functionalInfer _ T (app-inv-function $ INFER.proof p) of Î» ()
  ... | yes (Îº n   , _ , T) = no $ Î» p â†’ case functionalInfer _ T (app-inv-function $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ â”€o Ï„ , Î” , T)
    with check Î” Ïƒ u
  ... | no Â¬p = no $ Î» p â†’ let eq     = functionalInferPost _ (app-inv-function (INFER.proof p)) T
                               coerce = substâ‚‚ (_âŠ¢_âˆ‹ _ âŠ  _) (cong projâ‚‚ eq) (projâ‚ $ â”€o-inj $ cong projâ‚ eq)
                           in Â¬p (_ , coerce (app-inv-argument (INFER.proof p)))
  ... | yes (Î¸ , U) = yes (Ï„ , Î¸ , `app T U)

  -- SKIP
  infer Î“ (`skip u t)
    with check Î“ ğŸ™ u
  ... | no Â¬p = no $ Î» p â†’ Â¬p (_ , (projâ‚ $ projâ‚‚ $ skip-inv $ INFER.proof p))
  ... | yes (Î” , U) with infer Î” t
  ... | no Â¬q = no $ Î» q â†’
    let (Uâ€² P., Tâ€²) = projâ‚‚ $ skip-inv $ INFER.proof q
        eq          = functionalCheckPost _ U Uâ€²
    in Â¬q (_ , _ , subst (Î» Î” â†’ TInfer Î” _ _ _) (sym eq) Tâ€²)
  ... | yes (Î˜ , Ïƒ , T) = yes (Î˜ , Ïƒ , `skip U T)



  -- FST
  infer Î“ (`fst t)
    with infer Î“ t
  ... | no Â¬p = no $ Î» p â†’ Â¬p (_ , _ , projâ‚‚ (fst-inv (INFER.proof p)))
  ... | yes (ğŸ™ , _ , T)      = no $ Î» p â†’ case functionalInfer _ T (projâ‚‚ $ fst-inv $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ âŠ— Ï„ , _ , T)  = no $ Î» p â†’ case functionalInfer _ T (projâ‚‚ $ fst-inv $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ âŠ• Ï„ , _ , T)  = no $ Î» p â†’ case functionalInfer _ T (projâ‚‚ $ fst-inv $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ â”€o Ï„ , _ , T) = no $ Î» p â†’ case functionalInfer _ T (projâ‚‚ $ fst-inv $ INFER.proof p) of Î» ()
  ... | yes (Îº n   , _ , T)  = no $ Î» p â†’ case functionalInfer _ T (projâ‚‚ $ fst-inv $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ & Ï„ , Î” , T)  = yes (Ïƒ , Î” , `fst T)

  -- SND
  infer Î“ (`snd t)
    with infer Î“ t
  ... | no Â¬p = no $ Î» p â†’ Â¬p (_ , _ , projâ‚‚ (snd-inv (INFER.proof p)))
  ... | yes (ğŸ™ , _ , T)      = no $ Î» p â†’ case functionalInfer _ T (projâ‚‚ $ snd-inv $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ âŠ— Ï„ , _ , T)  = no $ Î» p â†’ case functionalInfer _ T (projâ‚‚ $ snd-inv $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ âŠ• Ï„ , _ , T)  = no $ Î» p â†’ case functionalInfer _ T (projâ‚‚ $ snd-inv $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ â”€o Ï„ , _ , T) = no $ Î» p â†’ case functionalInfer _ T (projâ‚‚ $ snd-inv $ INFER.proof p) of Î» ()
  ... | yes (Îº n   , _ , T)  = no $ Î» p â†’ case functionalInfer _ T (projâ‚‚ $ snd-inv $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ & Ï„ , Î” , T)  = yes (Ï„ , Î” , `snd T)


  -- CASE
  infer Î“ (`case t return Î½ of l %% r)
    with infer Î“ t
  ... | no Â¬p = no $ Î» p â†’ Â¬p (_ , _ , case-inv-scrutinee (INFER.proof p))
  ... | yes (ğŸ™  , _ , T)     = no $ Î» p â†’ case functionalInfer _ T (case-inv-scrutinee $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ âŠ— Ï„  , _ , T) = no $ Î» p â†’ case functionalInfer _ T (case-inv-scrutinee $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ & Ï„  , _ , T) = no $ Î» p â†’ case functionalInfer _ T (case-inv-scrutinee $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ â”€o Ï„ , _ , T) = no $ Î» p â†’ case functionalInfer _ T (case-inv-scrutinee $ INFER.proof p) of Î» ()
  ... | yes (Îº n    , _ , T) = no $ Î» p â†’ case functionalInfer _ T (case-inv-scrutinee $ INFER.proof p) of Î» ()
  ... | yes (Ïƒ âŠ• Ï„  , Î” , T)
    with check ([ Ïƒ ] âˆ· Î”) Î½ l | check ([ Ï„ ] âˆ· Î”) Î½ r
  ... | no Â¬l | _     = no $ Î» p â†’
    let eq     = functionalInferPost _ (case-inv-scrutinee $ INFER.proof p) T
        coerce = substâ‚‚ (Î» Î” Ïƒ â†’ [ Ïƒ ] âˆ· Î” âŠ¢ Î½ âˆ‹ l âŠ  ] Ïƒ [ âˆ· _) (cong projâ‚‚ eq) (projâ‚ $ âŠ•-inj $ cong projâ‚ eq)
    in Â¬l (_ , coerce (case-inv-left (INFER.proof p)))
  ... | _     | no Â¬r = no $ Î» p â†’
    let eq     = functionalInferPost _ (case-inv-scrutinee $ INFER.proof p) T
        coerce = substâ‚‚ (Î» Î” Ï„ â†’ [ Ï„ ] âˆ· Î” âŠ¢ Î½ âˆ‹ r âŠ  ] Ï„ [ âˆ· _) (cong projâ‚‚ eq) (projâ‚‚ $ âŠ•-inj $ cong projâ‚ eq)
    in Â¬r (_ , coerce (case-inv-right (INFER.proof p)))
  ... | yes ([ .Ïƒ ] âˆ· Î¸â‚ , L) | _ = no $ Î» p â†’
    let eq     = functionalInferPost _ (case-inv-scrutinee (INFER.proof p)) T
        coerce = substâ‚‚ (Î» Ïƒ Î¸ â†’ [ Ïƒ ] âˆ· Î¸ âŠ¢ Î½ âˆ‹ l âŠ  ] Ïƒ [ âˆ· _) (projâ‚ $ âŠ•-inj $ cong projâ‚ eq) (cong projâ‚‚ eq)
    in case functionalCheckPost _ (coerce (case-inv-left (INFER.proof p))) L of Î» ()
  ... | _ | yes ([ .Ï„ ] âˆ· Î¸â‚‚ , R) = no $ Î» p â†’
    let eq     = functionalInferPost _ (case-inv-scrutinee (INFER.proof p)) T
        coerce = substâ‚‚ (Î» Ïƒ Î¸ â†’ [ Ïƒ ] âˆ· Î¸ âŠ¢ Î½ âˆ‹ r âŠ  ] Ïƒ [ âˆ· _) (projâ‚‚ $ âŠ•-inj $ cong projâ‚ eq) (cong projâ‚‚ eq)
    in case functionalCheckPost _ (coerce (case-inv-right (INFER.proof p))) R of Î» ()
  ... | yes (] .Ïƒ [ âˆ· Î¸â‚ , L) | yes (] .Ï„ [ âˆ· Î¸â‚‚ , R)
    with eqs Î¸â‚ Î¸â‚‚
  ... | no Â¬eq = no $ Î» p â†’
    let eq      = functionalInferPost _ (case-inv-scrutinee (INFER.proof p)) T
        coerceË¡ = substâ‚‚ (Î» Ïƒ Î¸ â†’ [ Ïƒ ] âˆ· Î¸ âŠ¢ Î½ âˆ‹ l âŠ  ] Ïƒ [ âˆ· _) (projâ‚ $ âŠ•-inj $ cong projâ‚ eq) (cong projâ‚‚ eq)
        coerceÊ³ = substâ‚‚ (Î» Ïƒ Î¸ â†’ [ Ïƒ ] âˆ· Î¸ âŠ¢ Î½ âˆ‹ r âŠ  ] Ïƒ [ âˆ· _) (projâ‚‚ $ âŠ•-inj $ cong projâ‚ eq) (cong projâ‚‚ eq)
        eqâ‚     = functionalCheckPost _ (coerceË¡ (case-inv-left (INFER.proof p))) L
        eqâ‚‚     = functionalCheckPost _ (coerceÊ³ (case-inv-right (INFER.proof p))) R
    in Â¬eq $ trans (sym $ cong tail eqâ‚) (cong tail eqâ‚‚)
  ... | yes eq rewrite eq = yes (Î½ , _ , `case T return Î½ of L %% R)

  -- CUT
  infer Î“ (`cut t Ïƒ)                   = inferCut Î“ t Ïƒ <$> check Î“ Ïƒ t

  check : {n : â„•} {Î³ : Context n} (Î“ : Usages Î³) (Ïƒ : Type) (t : Check n) â†’ Dec $ CHECK Î“ Ïƒ t

  -- NEU
  check Î“ Ïƒ (`neu t)
    with infer Î“ t
  ... | no Â¬p = no $ Î» p â†’ case Â¬p (_ , _ , (neu-inv $ CHECK.proof p)) of Î» ()
  ... | yes (Ï„ , Î” , p)
    with Type.eq Ïƒ Ï„
  ... | no Â¬Ïƒâ‰¡Ï„ = no $ Î» q â†’ Â¬Ïƒâ‰¡Ï„ $ functionalInfer _ (neu-inv $ CHECK.proof q) p
  check Î“ Ïƒ (`neu t) | yes (.Ïƒ , Î” , p) | yes refl = yes (Î” , `neu p)

  check Î“ Ïƒ (`let p âˆ·= t `in u)
    with infer Î“ t
  ... | no Â¬p = no $ Î» p â†’ Â¬p (_ , _ , let-inv-bound (CHECK.proof p))
  ... | yes (Ï„ , Î” , T)
    with checkPattern Ï„ p
  ... | no Â¬p = no $ Î» p â†’ let eq     = functionalInfer _ (let-inv-bound (CHECK.proof p)) T
                               coerce = subst (_âˆ‹ _ â† (patternContext (let-inv-pattern (CHECK.proof p)))) eq
                           in Â¬p (_ , (coerce (let-inv-pattern (CHECK.proof p))))
  ... | yes (Î´ , P)
    with check ([[ Î´ ]] ++ Î”) Ïƒ u
  ... | no Â¬q = no $ Î» q â†’
    let eqâ‚     = functionalInferPost _ (let-inv-bound (CHECK.proof q)) T
        coerceâ‚ = subst (_âˆ‹ p â† patternContext (let-inv-pattern (CHECK.proof q))) (cong projâ‚ eqâ‚)
        eqâ‚‚     = functionalPattern _ (coerceâ‚ (let-inv-pattern (CHECK.proof q))) P
        coerceâ‚‚ = substâ‚‚ (Î» Î´ Î” â†’ [[ Î´ ]] ++ Î” âŠ¢ Ïƒ âˆ‹ u âŠ  ]] Î´ [[ ++ _) eqâ‚‚ (cong projâ‚‚ eqâ‚) 
    in Â¬q (_ , coerceâ‚‚ (let-inv-body (CHECK.proof q)))
  ... | yes (Î¸ , U)
    with truncate Î´ Î¸
  ... | no Â¬q = no $ Î» q â†’
    let eqâ‚     = functionalInferPost _ (let-inv-bound (CHECK.proof q)) T
        coerceâ‚ = subst (_âˆ‹ p â† patternContext (let-inv-pattern (CHECK.proof q))) (cong projâ‚ eqâ‚)
        eqâ‚‚     = functionalPattern _ (coerceâ‚ (let-inv-pattern (CHECK.proof q))) P
        coerceâ‚‚ = substâ‚‚ (Î» Î´ Î” â†’ [[ Î´ ]] ++ Î” âŠ¢ Ïƒ âˆ‹ u âŠ  ]] Î´ [[ ++ _) eqâ‚‚ (cong projâ‚‚ eqâ‚)
        eqâ‚ƒ     = functionalCheckPost _ (coerceâ‚‚ (let-inv-body (CHECK.proof q))) U
    in Â¬q (_ , sym eqâ‚ƒ)
  ... | yes (Î¾ , eq) rewrite eq = yes (_ , `let P âˆ·= T `in U)

  -- UNIT
  check Î“ ğŸ™        `unit = yes (Î“ , `unit)
  check Î“ (Îº k)    `unit = no (Î» { (_ , ()) })
  check Î“ (Ïƒ âŠ— Ï„)  `unit = no (Î» { (_ , ()) })
  check Î“ (Ïƒ â”€o Ï„) `unit = no (Î» { (_ , ()) })
  check Î“ (Ïƒ & Ï„)  `unit = no (Î» { (_ , ()) })
  check Î“ (Ïƒ âŠ• Ï„)  `unit = no (Î» { (_ , ()) })

  -- LAM
  check Î“ (Ïƒ â”€o Ï„) (`lam b)
    with check ([ Ïƒ ] âˆ· Î“) Ï„ b
  ... | no Â¬p                = no $ Î» p â†’ Â¬p (_ , lam-inv (CHECK.proof p))
  ... | yes ([ .Ïƒ ] âˆ· Î” , p) = no Î» q â†’ case functionalCheckPost _ p (lam-inv $ CHECK.proof q) of Î» ()
  ... | yes (] .Ïƒ [ âˆ· Î” , p) = yes (Î” , `lam p)
  check Î“ ğŸ™       (`lam b) = no $ Î» p â†’ case CHECK.proof p of Î» ()
  check Î“ (Ïƒ & Ï„) (`lam b) = no $ Î» p â†’ case CHECK.proof p of Î» ()
  check Î“ (Ïƒ âŠ• Ï„) (`lam b) = no $ Î» p â†’ case CHECK.proof p of Î» ()
  check Î“ (Ïƒ âŠ— Ï„) (`lam b) = no $ Î» p â†’ case CHECK.proof p of Î» ()
  check Î“ (Îº n)   (`lam b) = no $ Î» p â†’ case CHECK.proof p of Î» ()

  -- PRD
  check Î“ (Ïƒ âŠ— Ï„)  (`prd t u)
    with check Î“ Ïƒ t
  ... | no Â¬p = no $ Î» p â†’ Â¬p (_ , prd-inv-fst (CHECK.proof p))
  ... | yes (Î¸ , T)
    with check Î¸ Ï„ u
  ... | no Â¬p = no $ Î» p â†’ let eq     = functionalCheckPost _ (prd-inv-fst (CHECK.proof p)) T
                               coerce = subst (_âŠ¢ Ï„ âˆ‹ u âŠ  _) eq
                           in Â¬p (_ , coerce (prd-inv-snd (CHECK.proof p)))
  ... | yes (Î” , U) = yes (Î” , `prdâŠ— T U)
  check Î“ (Ïƒ & Ï„)  (`prd t u)
    with check Î“ Ïƒ t | check Î“ Ï„ u
  ... | no Â¬p | _ = no $ Î» p â†’ Â¬p (_ , projâ‚ (prd&-inv (CHECK.proof p)))
  ... | _ | no Â¬q = no $ Î» p â†’ Â¬q (_ , projâ‚‚ (prd&-inv (CHECK.proof p)))
  ... | yes (Î˜â‚ , p) | yes (Î˜â‚‚ , q)
    with eqs Î˜â‚ Î˜â‚‚
  ... | no Â¬eq = no $ Î» pq â†’
     let (pâ€² P., qâ€²) = prd&-inv (CHECK.proof pq)
         eqp         = functionalCheckPost _ p pâ€²
         eqq         = functionalCheckPost _ q qâ€²
     in Â¬eq (trans eqp (sym eqq))
  ... | yes eq rewrite eq = yes (Î˜â‚‚ , `prd& p q)

  check Î“ ğŸ™        (`prd t u) = no $ Î» p â†’ case CHECK.proof p of Î» ()
  check Î“ (Ïƒ âŠ• Ï„)  (`prd t u) = no $ Î» p â†’ case CHECK.proof p of Î» ()
  check Î“ (Ïƒ â”€o Ï„) (`prd t u) = no $ Î» p â†’ case CHECK.proof p of Î» ()
  check Î“ (Îº n)    (`prd t u) = no $ Î» p â†’ case CHECK.proof p of Î» ()

  -- INL
  check Î“ (Ïƒ âŠ• Ï„)  (`inl t) = checkInl Î“ t Ïƒ Ï„ <$> check Î“ Ïƒ t
  check Î“ ğŸ™        (`inl t) = no $ Î» p â†’ case CHECK.proof p of Î» ()
  check Î“ (Ïƒ âŠ— Ï„)  (`inl t) = no $ Î» p â†’ case CHECK.proof p of Î» ()
  check Î“ (Ïƒ â”€o Ï„) (`inl t) = no $ Î» p â†’ case CHECK.proof p of Î» ()
  check Î“ (Ïƒ & Ï„)  (`inl t) = no $ Î» p â†’ case CHECK.proof p of Î» ()
  check Î“ (Îº n)    (`inl t) = no $ Î» p â†’ case CHECK.proof p of Î» ()

  -- INR
  check Î“ (Ïƒ âŠ• Ï„)  (`inr t) = checkInr Î“ t Ïƒ Ï„ <$> check Î“ Ï„ t
  check Î“ ğŸ™        (`inr t) = no $ Î» p â†’ case CHECK.proof p of Î» ()
  check Î“ (Ïƒ âŠ— Ï„)  (`inr t) = no $ Î» p â†’ case CHECK.proof p of Î» ()
  check Î“ (Ïƒ â”€o Ï„) (`inr t) = no $ Î» p â†’ case CHECK.proof p of Î» ()
  check Î“ (Ïƒ & Ï„)  (`inr t) = no $ Î» p â†’ case CHECK.proof p of Î» ()
  check Î“ (Îº n)    (`inr t) = no $ Î» p â†’ case CHECK.proof p of Î» ()

